/*
 * Copyright 2017 LinkedIn Corp. Licensed under the BSD 2-Clause License (the "License"). See License in the project root for license information.
 */
import com.linkedin.gradle.build.DistributeTask

plugins {
  id "com.jfrog.artifactory"
  id "idea"
}

group = 'com.linkedin.cruisecontrol'

project.ext {
  pomConfig = {
    url "https://github.com/linkedin/cruise-control"
    licenses {
      license {
        name "BSD 2-CLAUSE LICENSE"
        url "https://opensource.org/licenses/BSD-2-Clause"
      }
    }
    developers {
      developer {
        name "Adem Efe Gencer"
        email "efegencer@gmail.com"
      }
      developer {
        name "Jiangjie (Becket) Qin"
        email "becket.qin@gmail.com"
      }
      developer {
        name "Sir Joel Koshy"
        email "jjkoshy@yahoo.com"
      }
    }
    scm {
      url "https://github.com/linkedin/cruise-control"
    }
  }
  buildVersionFileName = "cruise-control-version.properties"
  commitId = project.hasProperty('commitId') ? commitId : null
}


allprojects {

  repositories {
    mavenCentral()
    jcenter()
  }
}

subprojects {
  group = rootProject.group
  version = rootProject.version

  apply plugin: 'java'
  apply plugin: 'checkstyle'
  apply plugin: 'findbugs'

  test.dependsOn('checkstyleMain', 'checkstyleTest')

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  //code quality and inspections
  checkstyle {
    toolVersion = '8.1'
    ignoreFailures = false
    configFile = rootProject.file('checkstyle/checkstyle.xml')
  }

  findbugs {
    toolVersion = "3.0.1"
    excludeFilter = file("$rootDir/gradle/findbugs-exclude.xml")
    ignoreFailures = false
  }

  test.dependsOn('checkstyleMain', 'checkstyleTest', 'findbugsMain', 'findbugsTest')

  tasks.withType(FindBugs) {
    reports {
      xml.enabled (project.hasProperty('xmlFindBugsReport'))
      html.enabled (!project.hasProperty('xmlFindBugsReport'))
    }
  }

  jar {
    from "$rootDir/LICENSE"
    from "$rootDir/NOTICE"
  }

  test {
    useJUnit {}
    testLogging {
      events "passed", "failed", "skipped"
      exceptionFormat = 'full'
    }
    if (!project.hasProperty("maxParallelForks")) {
      maxParallelForks = Runtime.runtime.availableProcessors()
    }
  }
}

project(':cruise-control-core') {
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.artifactory'

  configurations {
    testOutput
  }

  dependencies {
    compile "org.slf4j:slf4j-api:1.7.26"
    compile 'junit:junit:4.12'
    compile 'org.apache.commons:commons-math3:3.6.1'

    testOutput sourceSets.test.output
  }

  publishing {
    publications {
      java(MavenPublication) {
        from components.java
        artifact sourcesJar
        artifact javadocJar
        pom.withXml {
          def root = asNode()
          root.appendNode('name', 'cruise-control-core')
          root.appendNode('description', 'cruise control core related')
          root.children().last() + rootProject.ext.pomConfig
        }
      }
    }
  }

  artifactoryPublish.dependsOn assemble
  artifactoryPublish.dependsOn publishToMavenLocal

  sourceSets {
    main {
      java {
        srcDirs = ['src/main/java']
      }
    }
    test {
      java {
        srcDirs = ['src/test/java']
      }
    }

  }

}

project(':cruise-control') {
  apply plugin: 'scala'
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.artifactory'

  //needed because our java classes depend on scala classes, so must be compiled by scala
  sourceSets {
    main {
      java {
        srcDirs = []
      }

      scala {
        srcDirs = ['src/main/java', 'src/main/scala']
      }
    }

    test {
      java {
        srcDirs = []
      }
      scala {
        srcDirs = ['src/test/java', 'src/test/scala']
      }
    }

  }

  dependencies {
    compile project(':cruise-control-metrics-reporter')
    compile project(':cruise-control-core')
    compile "org.slf4j:slf4j-api:1.7.26" // TODO: Upgrade log4j to log4j2 to use async logger.
    compile "org.apache.zookeeper:zookeeper:3.5.5"
    compile "org.apache.kafka:kafka_2.11:0.11.0.2"
    compile 'org.apache.kafka:kafka-clients:0.11.0.2'
    compile "org.scala-lang:scala-library:2.11.11"
    compile 'junit:junit:4.12'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'org.apache.httpcomponents:httpclient:4.5.8'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'org.eclipse.jetty:jetty-server:9.4.18.v20190429'
    compile 'org.eclipse.jetty:jetty-servlet:9.4.18.v20190429'
    compile 'io.dropwizard.metrics:metrics-core:3.2.6'

    testCompile project(path: ':cruise-control-metrics-reporter', configuration: 'testOutput')
    testCompile project(path: ':cruise-control-core', configuration: 'testOutput')
    testCompile "org.scala-lang:scala-library:2.11.11"
    testCompile 'org.easymock:easymock:4.0.2'
    testCompile 'com.linkedin.kafka.clients:li-apache-kafka-clients:0.0.12'
    testCompile 'com.linkedin.kafka.clients:li-apache-kafka-clients:0.0.12:tests'
    testCompile 'org.apache.kafka:kafka_2.11:0.11.0.2:test'
    testCompile 'org.apache.kafka:kafka-clients:0.11.0.2:test'
    testCompile 'commons-io:commons-io:2.6'
  }

  publishing {
    publications {
      java(MavenPublication) {
        from components.java
        artifact sourcesJar
        artifact javadocJar
        pom.withXml {
          def root = asNode()
          root.appendNode('name', 'cruise-control')
          root.appendNode('description', 'cruise control related')
          root.children().last() + rootProject.ext.pomConfig
        }
      }
    }
  }

  artifactoryPublish.dependsOn assemble
  artifactoryPublish.dependsOn publishToMavenLocal

  tasks.create(name: "copyDependantLibs", type: Copy) {
    from (configurations.testRuntime) {
      include('slf4j-log4j12*')
    }
    from (configurations.runtime) {

    }
    into "$buildDir/dependant-libs"
    duplicatesStrategy 'exclude'
  }

  tasks.create(name: "buildFatJar", type: Jar) {
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
  }

  compileScala.finalizedBy(tasks.copyDependantLibs)

  task determineCommitId {
    def takeFromHash = 40
    if (commitId) {
      commitId = commitId.take(takeFromHash)
    } else if (file("$rootDir/.git/HEAD").exists()) {
      def headRef = file("$rootDir/.git/HEAD").text
      if (headRef.contains('ref: ')) {
        headRef = headRef.replaceAll('ref: ', '').trim()
        if (file("$rootDir/.git/$headRef").exists()) {
          commitId = file("$rootDir/.git/$headRef").text.trim().take(takeFromHash)
        }
      } else {
        commitId = headRef.trim().take(takeFromHash)
      }
    } else {
      commitId = "unknown"
    }
  }

  // Referenced similar method for getting software version in Kafka code.
  task createVersionFile(dependsOn: determineCommitId) {
    ext.receiptFile = file("$buildDir/cruise-control/$buildVersionFileName")
    outputs.file receiptFile
    outputs.upToDateWhen { false }
    doLast {
      def data = [
          commitId: commitId,
          version: version,
      ]

      receiptFile.parentFile.mkdirs()
      def content = data.entrySet().collect { "$it.key=$it.value" }.sort().join("\n")
      receiptFile.setText(content, "ISO-8859-1")
    }
  }

  jar {
    dependsOn createVersionFile
    from("$buildDir") {
      include "cruise-control/$buildVersionFileName"
    }
  }
}

project(':cruise-control-metrics-reporter') {
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.artifactory'

  configurations {
    testOutput
  }

  dependencies {
    compile "org.slf4j:slf4j-api:1.7.26"
    compile "org.apache.kafka:kafka_2.11:0.11.0.2"
    compile 'org.apache.kafka:kafka-clients:0.11.0.2'
    compile 'junit:junit:4.12'
    compile 'org.apache.commons:commons-math3:3.6.1'

    testCompile 'org.bouncycastle:bcpkix-jdk15on:1.62'
    testCompile 'com.linkedin.kafka.clients:li-apache-kafka-clients:0.0.12'
    testCompile 'com.linkedin.kafka.clients:li-apache-kafka-clients:0.0.12:tests'
    testCompile 'org.apache.kafka:kafka-clients:0.11.0.2:test'
    testCompile 'org.apache.kafka:kafka-clients:0.11.0.2'
    testCompile 'commons-io:commons-io:2.6'
    testOutput sourceSets.test.output
  }

  publishing {
    publications {
      java(MavenPublication) {
        from components.java
        artifact sourcesJar
        artifact javadocJar
        pom.withXml {
          def root = asNode()
          root.appendNode('name', 'cruise-control-metrics-reporter')
          root.appendNode('description', 'cruise control metrics reporter related')
          root.children().last() + rootProject.ext.pomConfig
        }
      }
    }
  }

  artifactoryPublish.dependsOn assemble
  artifactoryPublish.dependsOn publishToMavenLocal

}

artifactoryPublish.skip = true
artifactory {
  contextUrl = 'https://linkedin.jfrog.io/linkedin'
  publish {
    repoKey = 'cruise-control'
    username = System.getenv('ARTIFACTORY_USER')
    password = System.getenv('ARTIFACTORY_KEY')

    defaults {
      publications ('java')
      publishBuildInfo = true
      publishArtifacts = true
      publishPom = true
      publishIvy = true
    }
  }
  clientConfig.setIncludeEnvVars(false)
}

task distributeBuild(type: DistributeTask) {
  dependsOn ':artifactoryPublish', ':cruise-control:artifactoryPublish', ':cruise-control-core:artifactoryPublish', ':cruise-control-metrics-reporter:artifactoryPublish'
}

//wrapper generation task
wrapper {
  gradleVersion = '5.4.1'
  distributionType = Wrapper.DistributionType.ALL
}