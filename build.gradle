/*
 * Copyright 2017 LinkedIn Corp. Licensed under the BSD 2-Clause License (the "License"). See License in the project root for license information.
 */

plugins {
  id "com.jfrog.artifactory"
  id "idea"
}

group = 'com.linkedin.cruisecontrol'

project.ext {
  pomConfig = {
    url "https://github.com/linkedin/cruise-control"
    licenses {
      license {
        name "BSD 2-CLAUSE LICENSE"
        url "https://opensource.org/licenses/BSD-2-Clause"
      }
    }
    developers {
      developer {
        name "Adem Efe Gencer"
        email "efegencer@gmail.com"
      }
      developer {
        name "Jiangjie (Becket) Qin"
        email "becket.qin@gmail.com"
      }
      developer {
        name "Sir Joel Koshy"
        email "jjkoshy@yahoo.com"
      }
    }
    scm {
      url "https://github.com/linkedin/cruise-control"
    }
  }
}


allprojects {

  repositories {
    mavenCentral()
    jcenter()
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'checkstyle'
  apply plugin: 'findbugs'

  test.dependsOn('checkstyleMain', 'checkstyleTest')

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  //code quality and inspections
  checkstyle {
    toolVersion = '7.5.1'
    ignoreFailures = false
    configFile = rootProject.file('checkstyle/checkstyle.xml')
  }

  findbugs {
    toolVersion = "3.0.1"
    excludeFilter = file("$rootDir/gradle/findbugs-exclude.xml")
    ignoreFailures = false
  }

  test.dependsOn('checkstyleMain', 'checkstyleTest', 'findbugsMain', 'findbugsTest')

  tasks.withType(FindBugs) {
    reports {
      xml.enabled (project.hasProperty('xmlFindBugsReport'))
      html.enabled (!project.hasProperty('xmlFindBugsReport'))
    }
  }

  jar {
    from "$rootDir/LICENSE"
    from "$rootDir/NOTICE"
  }

  test {
    useJUnit {}
    testLogging {
      events "passed", "failed", "skipped"
      exceptionFormat = 'full'
    }
    if (!project.hasProperty("maxParallelForks")) {
      maxParallelForks = Runtime.runtime.availableProcessors()
    }
  }
}

project(':cruise-control-core') {
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.artifactory'

  configurations {
    testOutput
  }

  dependencies {
    compile "org.slf4j:slf4j-api:1.7.25"
    compile 'junit:junit:4.12'
    compile 'org.apache.commons:commons-math3:3.6.1'

    testOutput sourceSets.test.output
  }

  publishing {
    publications {
      java(MavenPublication) {
        from components.java
        artifact sourcesJar
        artifact javadocJar
        pom.withXml {
          def root = asNode()
          root.appendNode('name', 'cruise-control-core')
          root.appendNode('description', 'cruise control core related')
          root.children().last() + rootProject.ext.pomConfig
        }
      }
    }
  }

  artifactoryPublish.dependsOn assemble
  artifactoryPublish.dependsOn publishToMavenLocal

  sourceSets {
    main {
      java {
        srcDirs = ['src/main/java']
      }
    }
    test {
      java {
        srcDirs = ['src/test/java']
      }
    }

  }

}

project(':cruise-control') {
  apply plugin: 'scala'
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.artifactory'

  //needed because our java classes depend on scala classes, so must be compiled by scala
  sourceSets {
    main {
      java {
        srcDirs = []
      }

      scala {
        srcDirs = ['src/main/java', 'src/main/scala']
      }
    }

    test {
      java {
        srcDirs = []
      }
      scala {
        srcDirs = ['src/test/java', 'src/test/scala']
      }
    }

  }

  dependencies {
    compile project(':cruise-control-metrics-reporter')
    compile project(':cruise-control-core')
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "org.apache.zookeeper:zookeeper:3.4.6"
    compile "org.apache.kafka:kafka_2.11:0.11.0.2"
    compile 'org.apache.kafka:kafka-clients:0.11.0.2'
    compile "org.scala-lang:scala-library:2.11.11"
    compile 'junit:junit:4.12'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'com.google.code.gson:gson:2.7'
    compile 'org.eclipse.jetty:jetty-server:9.4.6.v20170531'
    compile 'org.eclipse.jetty:jetty-servlet:9.4.6.v20170531'
    compile 'io.dropwizard.metrics:metrics-core:3.2.2'

    testCompile project(path: ':cruise-control-metrics-reporter', configuration: 'testOutput')
    testCompile project(path: ':cruise-control-core', configuration: 'testOutput')
    testCompile "org.scala-lang:scala-library:2.11.11"
    testCompile 'org.easymock:easymock:3.4'
    testCompile 'com.linkedin.kafka.clients:li-apache-kafka-clients:0.0.12'
    testCompile 'com.linkedin.kafka.clients:li-apache-kafka-clients:0.0.12:tests'
    testCompile 'org.apache.kafka:kafka_2.11:0.11.0.2:test'
    testCompile 'org.apache.kafka:kafka-clients:0.11.0.2:test'
    testCompile 'commons-io:commons-io:2.5'
  }

  publishing {
    publications {
      java(MavenPublication) {
        from components.java
        artifact sourcesJar
        artifact javadocJar
        pom.withXml {
          def root = asNode()
          root.appendNode('name', 'cruise-control')
          root.appendNode('description', 'cruise control related')
          root.children().last() + rootProject.ext.pomConfig
        }
      }
    }
  }

  artifactoryPublish.dependsOn assemble
  artifactoryPublish.dependsOn publishToMavenLocal

  tasks.create(name: "copyDependantLibs", type: Copy) {
    from (configurations.testRuntime) {
      include('slf4j-log4j12*')
    }
    from (configurations.runtime) {

    }
    into "$buildDir/dependant-libs"
    duplicatesStrategy 'exclude'
  }

  compileScala.doLast {
    tasks.copyDependantLibs.execute()
  }
}

project(':cruise-control-metrics-reporter') {
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.artifactory'

  configurations {
    testOutput
  }

  dependencies {
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "org.apache.kafka:kafka_2.11:0.11.0.2"
    compile 'org.apache.kafka:kafka-clients:0.11.0.2'
    compile 'junit:junit:4.12'
    compile 'org.apache.commons:commons-math3:3.6.1'

    testCompile 'org.bouncycastle:bcpkix-jdk15on:1.54'
    testCompile 'com.linkedin.kafka.clients:li-apache-kafka-clients:0.0.12'
    testCompile 'com.linkedin.kafka.clients:li-apache-kafka-clients:0.0.12:tests'
    testCompile 'org.apache.kafka:kafka-clients:0.11.0.2:test'
    testCompile 'org.apache.kafka:kafka-clients:0.11.0.2'
    testCompile 'commons-io:commons-io:2.5'
    testOutput sourceSets.test.output
  }

  publishing {
    publications {
      java(MavenPublication) {
        from components.java
        artifact sourcesJar
        artifact javadocJar
        pom.withXml {
          def root = asNode()
          root.appendNode('name', 'cruise-control-metrics-reporter')
          root.appendNode('description', 'cruise control metrics reporter related')
          root.children().last() + rootProject.ext.pomConfig
        }
      }
    }
  }

  artifactoryPublish.dependsOn assemble
  artifactoryPublish.dependsOn publishToMavenLocal

}

artifactoryPublish.skip = true
artifactory {
  contextUrl = 'https://linkedin.jfrog.io/linkedin'
  publish {
    repoKey = 'cruise-control'
    username = System.getenv('ARTIFACTORY_USER')
    password = System.getenv('ARTIFACTORY_KEY')

    defaults {
      publications ('java')
      publishBuildInfo = true
      publishArtifacts = true
      publishPom = true
      publishIvy = true
    }
  }
  clientConfig.setIncludeEnvVars(false)
}

task distributeBuild(type: com.linkedin.gradle.build.DistributeTask) {
  dependsOn ':artifactoryPublish', ':cruise-control:artifactoryPublish', ':cruise-control-core:artifactoryPublish', ':cruise-control-metrics-reporter:artifactoryPublish'
}

//wrapper generation task
task wrapper(type: Wrapper) {
  gradleVersion = '4.6'
  distributionType = Wrapper.DistributionType.ALL
}